//= require gmapsjs
//= require oms
//= require geolocationmarker
//= require jquery.typewatch

window.console and window.console.info('O, Å¾ivjo, kaj pa ti tukaj? API je na voljo na http://boni.mr.si/api/restaurants, source pa na https://github.com/mrfoto/studentska-prehrana ;)')

$ ->
  restaurants = filteredRestaurants = filteredIDs = features = search = featureToggle = latestMarker = request = fitMarkersTimer = geoMarker = null

  getMarkerIcon = (image) ->
    new google.maps.MarkerImage(image, null, null, null, new google.maps.Size(18,31));

  displayRestaurants = ->
    if !restaurants?.length then return
    map.removeMarkers()
    filteredRestaurants = if filteredIDs then restaurants.filter (restaurant) -> restaurant['id'] in filteredIDs else restaurants
    for restaurant in filteredRestaurants
      marker = map.addMarker
        lat: restaurant['coordinates'][0]
        lng: restaurant['coordinates'][1]
        icon: scaleMarkers[restaurant['price'][0]]
        shadow: new google.maps.MarkerImage('<%= asset_path("markers/shadow.png") %>', null, null, new google.maps.Point(9,34), new google.maps.Size(31,34))
        content: restaurant['content']
      oms.addMarker marker
    fitMarkers()
    if filteredRestaurants.length == 1 then google.maps.event.trigger(marker, 'click')
    
  fitMarkers = ->
    clearTimeout fitMarkersTimer
    fitMarkersTimer = setTimeout(->
        map.refresh()
        if filteredRestaurants.length and $('#adapt-bounds').is(':checked') then map.fitZoom()
      , 300)

  searchForRestaurants = ->
    if request? then request.abort()
    if search?.length or features?.length
      request = $.getJSON '/search',
        search: search
        features: features
      , (data) ->
        filteredIDs = data
        displayRestaurants()
    else
      filteredIDs = null
      displayRestaurants()

  showGeoMarker = ->
    geoMarker = new GeolocationMarker map.map
    geoMarker.setMinimumAccuracy 100
    google.maps.event.addListenerOnce geoMarker, 'position_changed', ->
      map.addControl
        position: 'top_right'
        content: 'Kje sem?'
        style:
          margin: '5px'
          padding: '2px 6px'
          border: 'solid 1px #717B87'
          background: '#fff'
        events:
          click: ->
            $('#adapt-bounds').prop('checked', false)
            map.panTo geoMarker.getPosition()
            if map.getZoom() < 16 then map.setZoom 16
            map.refresh()
  
  showFeatureToggle = ->
    if $(window).width() < 768
      if !featureToggle?
        $('#adapt-bounds').prop('checked', false)
        featureToggle = map.addControl
          position: 'top_left'
          content: 'Iskalnik'
          style:
            margin: '5px'
            padding: '2px 6px'
            border: 'solid 1px #717B87'
            background: '#fff'
          events:
            click: ->
              if $('body').hasClass('active-nav')
                $('body').removeClass('active-nav')
                $(featureToggle).text('Iskalnik')
                $('#adapt-bounds').prop('checked', false)
              else
                $('body').addClass('active-nav')
                $(featureToggle).text('Skrij iskalnik')
                $('#adapt-bounds').prop('checked', true)
              fitMarkers()
      $(featureToggle).show()
    else if featureToggle?
      $(featureToggle).hide()
      $(featureToggle).text('Iskalnik')
      $('body').removeClass('active-nav')

  map = new GMaps(
    div: '#map'
    lat: 46.119944
    lng: 14.815333
    zoom: 8
    minZoom: 6
    disableDefaultUI: true
  )

  oms = new OverlappingMarkerSpiderfier(map.map,
    keepSpiderfied: true
    markersWontMove: true
    markersWontHide: true
  )

  iw = new google.maps.InfoWindow()
  oms.addListener 'click', (marker) ->
    iw.setContent marker.content
    latestMarker = marker
    iw.open map.map, marker

  scaleMarkers = [getMarkerIcon('<%= asset_path("markers/ffe7c8.png") %>'), getMarkerIcon('<%= asset_path("markers/ffcc95.png") %>'), getMarkerIcon('<%= asset_path("markers/ffad60.png") %>'), getMarkerIcon('<%= asset_path("markers/ff8c1f.png") %>'), getMarkerIcon('<%= asset_path("markers/e04f00.png") %>')]

  $.getJSON '/all_restaurants', (data) ->
    restaurants = data
    displayRestaurants()
    if navigator.geolocation then showGeoMarker()
  
  showFeatureToggle()
  
  $('#map').on 'click', '.load-menu', (event) ->
    event.preventDefault()
    $this = $(this)
    $.get '/menu',
      restaurant: $this.data('restaurant')
    , ((data) ->
      $this.replaceWith data
      iw.setContent $('.prehrana-info').parent()[0]
      iw.open map.map, latestMarker
    ), 'html'

  $('#restaurant-search').typeWatch
    callback: (val) ->
      search = val
      searchForRestaurants()
    captureLength: 2
    
  $('#filter li').on 'click', (event) ->
    if event.target == this
      $(this).find('input').click()

  $('.feature-filter').on 'change', () ->
    features = []
    $('.feature-filter').each ->
      if $(this).is(':checked') then features.push $(this).data('feature-id')
    if features.length
      $('#all-switch').prop('checked', false).prop('disabled', false)
    else
      $('#all-switch').prop('checked', true).prop('disabled', true)
    searchForRestaurants()

  $('#all-switch').on 'change', () ->
    $this = $(this)
    if $this.is(':checked')
      features = []
      $this.prop('disabled', true)
      $('.feature-filter').each ->
        $(this).prop('checked', false)
    searchForRestaurants()
    
  $('#adapt-bounds').on 'change', () ->
    $this = $(this)
    if $this.is(':checked')
      fitMarkers()
      
  $(document).scroll ->
    if $(document).scrollLeft() > 100 then $(featureToggle).click()
  
  $(window).resize ->
    showFeatureToggle()
    fitMarkers()