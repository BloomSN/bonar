//= require gmapsjs
//= require oms
//= require geolocationmarker

$ ->
  restaurants = filteredIDs = features = search = latestMarker = timer = GeoMarker = null
  
  getMarkerIcon = (image) ->
    new google.maps.MarkerImage(image, null, null, null, new google.maps.Size(18,31));  
  
  displayRestaurants = ->
    map.removeMarkers()
    filteredRestaurants = if filteredIDs then restaurants.filter (restaurant) -> restaurant['id'] in filteredIDs else restaurants
    
    for restaurant in filteredRestaurants  
      marker = map.addMarker
        lat: restaurant['coordinates'][0]
        lng: restaurant['coordinates'][1]
        icon: scaleMarkers[restaurant['price'][0]]
        shadow: new google.maps.MarkerImage('<%= asset_path("markers/shadow.png") %>', null, null, new google.maps.Point(9,34), new google.maps.Size(31,34))
        content: restaurant['content']
      oms.addMarker(marker);   
      
    if filteredRestaurants.length == 1 or (filteredRestaurants.length == 2 and filteredRestaurants[0]['coordinates'][0] == filteredRestaurants[1]['coordinates'][0] and filteredRestaurants[0]['coordinates'][1] == filteredRestaurants[1]['coordinates'][1])
      map.setZoom 15
      map.panTo marker.position
      google.maps.event.trigger(marker, 'click')

  searchForRestaurants = ->
    if search or features
      $.getJSON '/search',
        search: search
        features: features
      , (data) ->
        filteredIDs = data
        if restaurants
          displayRestaurants()
    else
      filteredIDs = null
      if restaurants
        displayRestaurants()

  showGeoMarker = ->
    GeoMarker = new GeolocationMarker map.map
    GeoMarker.setMinimumAccuracy 100
    google.maps.event.addListenerOnce GeoMarker, 'position_changed', ->
      map.setZoom 15
      map.panTo @getPosition()
      $('#geolocateIcon').removeClass('hidden')
  
  map = new GMaps(
    div: '#map'
    lat: 46.119944
    lng: 14.815333
    zoom: 8
    disableDefaultUI: true
  )
  
  oms = new OverlappingMarkerSpiderfier(map.map,
    keepSpiderfied: true
    markersWontMove: true
    markersWontHide: true
  )
  
  iw = new google.maps.InfoWindow()
  oms.addListener 'click', (marker) ->
    iw.setContent marker.content
    latestMarker = marker
    iw.open map.map, latestMarker
    
  scaleMarkers = [getMarkerIcon('<%= asset_path("markers/ffe7c8.png") %>'), getMarkerIcon('<%= asset_path("markers/ffcc95.png") %>'), getMarkerIcon('<%= asset_path("markers/ffad60.png") %>'), getMarkerIcon('<%= asset_path("markers/ff8c1f.png") %>'), getMarkerIcon('<%= asset_path("markers/e04f00.png") %>')]
  
  $.getJSON '/all_restaurants', (data) ->
    restaurants = data
    displayRestaurants()

  if navigator.geolocation
    showGeoMarker()
    
  $('#geolocateIcon').on 'click', (event) ->
    event.preventDefault()
    map.panTo GeoMarker.getPosition()
  
  $('#restaurantSearch').on 'search', ->
    clearTimeout timer
    search = $(this).val()
    searchForRestaurants()
      
  $('#map').on 'click', '.loadMenu', (event) ->
    $this = $(this)
    event.preventDefault()
    $.get '/menu',
      restaurant: $this.data('restaurant')
    , ((data) ->
      $this.replaceWith data
      iw.setContent $('.prehrana_info').parent()[0]
      iw.open map.map, latestMarker
    ), 'html'
  
  $('.feature-filter').on 'change', () ->
    features = []
    $('.feature-filter').each ->
      $this = $(this)
      if $this.is(':checked')
        features.push $(this).data('feature-id')
    if features.length
      $('#all_switch').prop('checked', false)
    else
      $('#all_switch').prop('checked', true)
    searchForRestaurants()
    
  $('#all_switch').on 'change', () ->
    if $(this).is(':checked')
      features = []
      $('.feature-filter').each ->
        $(this).prop('checked', false)
    searchForRestaurants()
      
window.console and window.console.info('O, Å¾ivjo, kaj pa ti tukaj? API je na voljo na http://boni.mr.si/api/restaurants, source pa na https://github.com/mrfoto/studentska-prehrana ;)')  